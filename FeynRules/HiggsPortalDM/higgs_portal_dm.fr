M$ModelName = "Standard Model + Higgs-Portal DM";

M$Information = {
  Authors      -> {"Logan A. Morrison"},
  Version      -> "1.0.0",
  Date         -> "24. 01. 2020",
  Institutions -> {"University of California, Santa Cruz"},
  Emails       -> {"loanmorr@ucsc.edu"}
};

FeynmanGauge = True;

M$vevs = { {Phi[2],vev} };


M$GaugeGroups = {
  U1Y  == {
    Abelian          -> True,
    CouplingConstant -> g1,
    GaugeBoson       -> B,
    Charge           -> Y
  },
  SU2L == {
    Abelian           -> False,
    CouplingConstant  -> gw,
    GaugeBoson        -> Wi,
    StructureConstant -> Eps,
    Representations   -> {Ta,SU2D},
    Definitions       -> {Ta[a_,b_,c_]->PauliSigma[a,b,c]/2, FSU2L[i_,j_,k_]:> I Eps[i,j,k]}
  },
  SU3C == {
    Abelian           -> False,
    CouplingConstant  -> gs,
    GaugeBoson        -> G,
    StructureConstant -> f,
    Representations   -> {T,Colour},
    SymmetricTensor   -> dSUN
  }
};


IndexRange[Index[SU2W      ]] = Unfold[Range[3]];
IndexRange[Index[SU2D      ]] = Unfold[Range[2]];
IndexRange[Index[Gluon     ]] = NoUnfold[Range[8]];
IndexRange[Index[Colour    ]] = NoUnfold[Range[3]];
IndexRange[Index[Generation]] = Range[3];

IndexStyle[SU2W,       j];
IndexStyle[SU2D,       k];
IndexStyle[Gluon,      a];
IndexStyle[Colour,     m];
IndexStyle[Generation, f];


(* ************************** *)
(* *** Interaction orders *** *)
(* ***  (as used by mg5)  *** *)
(* ************************** *)

M$InteractionOrderHierarchy = {
  {QCD, 1},
  {NP,  1},
  {NPMIX,  1},
  {QED, 2}
};

M$InteractionOrderLimit = {
  {NP, 4},
  {NPMIX, 4}
}


(* ************************** *)
(* **** Particle classes **** *)
(* ************************** *)
M$ClassesDescription = {
    (**** Physical Fields ****)

    (* SM photon *)
    V[1] == {
        ClassName       -> A,
        SelfConjugate   -> True,
        Mass            -> 0,
        Width           -> 0,
        ParticleName    -> "a",
        PDG             -> 22,
        PropagatorLabel -> "a",
        PropagatorType  -> W,
        PropagatorArrow -> None,
        FullName        -> "Photon"
    },
    (* Z-Boson *)
    V[2] == {
        ClassName       -> Z,
        SelfConjugate   -> True,
        Mass            -> {MZ, 91.1876},
        Width           -> {WZ, 2.4952},
        ParticleName    -> "Z",
        PDG             -> 23,
        PropagatorLabel -> "Z",
        PropagatorType  -> Sine,
        PropagatorArrow -> None,
        FullName        -> "Z"
    },
    (* W-Boson *)
    V[3] == {
        ClassName        -> W,
        SelfConjugate    -> False,
        Mass             -> {MW, Internal},
        Width            -> {WW, 2.085},
        ParticleName     -> "W+",
        AntiParticleName -> "W-",
        QuantumNumbers   -> {Q -> 1},
        PDG              -> 24,
        PropagatorLabel  -> "W",
        PropagatorType   -> Sine,
        PropagatorArrow  -> Forward,
        FullName         -> "W"
    },
    (* Gluon *)
    V[4] == {
        ClassName        -> G,
        SelfConjugate    -> True,
        Indices          -> {Index[Gluon]},
        Mass             -> 0,
        Width            -> 0,
        ParticleName     -> "g",
        PDG              -> 21,
        PropagatorLabel  -> "G",
        PropagatorType   -> C,
        PropagatorArrow  -> None,
        FullName         -> "G"
    },
    (* Photon ghost *)
    U[1] == {
        ClassName       -> ghA,
        SelfConjugate   -> False,
        Ghost           -> A,
        QuantumNumbers  -> {GhostNumber -> 1},
        Mass            -> 0,
        Width	    -> 0,
        PropagatorLabel -> "uA",
        PropagatorType  -> GhostDash,
        PropagatorArrow -> Forward
    },
    (* Z-boson ghost *)
    U[2] == {
        ClassName       -> ghZ,
        SelfConjugate   -> False,
        Ghost           -> Z,
        QuantumNumbers  -> {GhostNumber -> 1},
        Mass            -> {MZ,91.1876},
        Width	    -> {WZ, 2.4952},
        PropagatorLabel -> "uZ",
        PropagatorType  -> GhostDash,
        PropagatorArrow -> Forward
    },
    (* w+ ghost *)
    U[31] == {
        ClassName       -> ghWp,
        SelfConjugate   -> False,
        Ghost           -> W,
        QuantumNumbers  -> {GhostNumber -> 1, Q -> 1},
        Mass            -> {MW,Internal},
        Width           -> {WW, 2.085},
        PropagatorLabel -> "uWp",
        PropagatorType  -> GhostDash,
        PropagatorArrow -> Forward
    },
    (* w- ghost *)
    U[32] == {
        ClassName       -> ghWm,
        SelfConjugate   -> False,
        Ghost           -> Wbar,
        QuantumNumbers  -> {GhostNumber -> 1, Q -> -1},
        Mass            -> {MW,Internal},
        Width           -> {WW, 2.085},
        PropagatorLabel -> "uWm",
        PropagatorType  -> GhostDash,
        PropagatorArrow -> Forward
    },
    (* gluon ghost *)
    U[4] == {
        ClassName       -> ghG,
        SelfConjugate   -> False,
        Indices         -> {Index[Gluon]},
        Ghost           -> G,
        PDG             -> 82,
        QuantumNumbers  ->{GhostNumber -> 1},
        Mass            -> 0,
        Width	    -> 0,
        PropagatorLabel -> "uG",
        PropagatorType  -> GhostDash,
        PropagatorArrow -> Forward
    },
    (* Up-type leptons: Neutrinos *)
    F[1] == {
        ClassName        -> vl,
        ClassMembers     -> {ve,vm,vt},
        Indices          -> {Index[Generation]},
        FlavorIndex      -> Generation,
        SelfConjugate    -> False,
        Mass             -> 0,
        Width            -> 0,
        QuantumNumbers   -> {LeptonNumber -> 1},
        PropagatorLabel  -> {"v", "ve", "vm", "vt"} ,
        PropagatorType   -> S,
        PropagatorArrow  -> Forward,
        PDG              -> {12,14,16},
        ParticleName     -> {"ve","vm","vt"},
        AntiParticleName -> {"ve~","vm~","vt~"},
        FullName         -> {"Electron-neutrino", "Mu-neutrino", "Tau-neutrino"}
    },
    (* Down-type leptons: charged lepton *)
    F[2] == {
        ClassName        -> l,
        ClassMembers     -> {e, mu, ta},
        Indices          -> {Index[Generation]},
        FlavorIndex      -> Generation,
        SelfConjugate    -> False,
        Mass             -> {Ml, {Me,5.11*^-4}, {MMU,0.10566}, {MTA,1.777}},
        Width            -> 0,
        QuantumNumbers   -> {Q -> -1, LeptonNumber -> 1},
        PropagatorLabel  -> {"l", "e", "mu", "ta"},
        PropagatorType   -> Straight,
        PropagatorArrow  -> Forward,
        PDG              -> {11, 13, 15},
        ParticleName     -> {"e-", "mu-", "ta-"},
        AntiParticleName -> {"e+", "mu+", "ta+"},
        FullName         -> {"Electron", "Muon", "Tau"}
    },
    (* up-type quarks *)
    F[3] == {
        ClassName        -> uq,
        ClassMembers     -> {u, c, t},
        Indices          -> {Index[Generation], Index[Colour]},
        FlavorIndex      -> Generation,
        SelfConjugate    -> False,
        Mass             -> {Mu, {MU, 2.55*^-3}, {MC,1.27}, {MT,172}},
        Width            -> {0, 0, {WT,1.50833649}},
        QuantumNumbers   -> {Q -> 2/3},
        PropagatorLabel  -> {"uq", "u", "c", "t"},
        PropagatorType   -> Straight,
        PropagatorArrow  -> Forward,
        PDG              -> {2, 4, 6},
        ParticleName     -> {"u",  "c",  "t" },
        AntiParticleName -> {"u~", "c~", "t~"},
        FullName         -> {"u-quark", "c-quark", "t-quark"}
    },
    (* down-type quarks *)
    F[4] == {
        ClassName        -> dq,
        ClassMembers     -> {d, s, b},
        Indices          -> {Index[Generation], Index[Colour]},
        FlavorIndex      -> Generation,
        SelfConjugate    -> False,
        Mass             -> {Md, {MD,5.04*^-3}, {MS,0.101}, {MB,4.7}},
        Width            -> 0,
        QuantumNumbers   -> {Q -> -1/3},
        PropagatorLabel  -> {"dq", "d", "s", "b"},
        PropagatorType   -> Straight,
        PropagatorArrow  -> Forward,
        PDG              -> {1,3,5},
        ParticleName     -> {"d",  "s",  "b" },
        AntiParticleName -> {"d~", "s~", "b~"},
        FullName         -> {"d-quark", "s-quark", "b-quark"}
    },
    (* Fermionic dark matter *)
    F[5] == {
        ClassName        -> chi,
        SelfConjugate    -> False,
        Mass             -> {MDM, 5000.0},
        Width            -> 0,
        PropagatorLabel  -> "~chi",
        PropagatorType   -> Straight,
        PropagatorArrow  -> Forward,
        PDG              -> 52,
        ParticleName     -> "~chi",
        AntiParticleName -> "~chibar",
        FullName         -> "dark-matter"
    },

    (* Higgs-boson *)
    S[1] == {
        ClassName       -> H,
        SelfConjugate   -> True,
        Mass            -> {MH,125},
        Width           -> {WH,0.00407},
        PropagatorLabel -> "H",
        PropagatorType  -> D,
        PropagatorArrow -> None,
        PDG             -> 25,
        ParticleName    -> "H",
        FullName        -> "H"
    },
    (* Z-boson goldstone *)
    S[2] == {
        ClassName       -> G0,
        SelfConjugate   -> True,
        Goldstone       -> Z,
        Mass            -> {MZ, 91.1876},
        Width           -> {WZ, 2.4952},
        PropagatorLabel -> "Go",
        PropagatorType  -> D,
        PropagatorArrow -> None,
        PDG             -> 250,
        ParticleName    -> "G0",
        FullName        -> "G0"
    },
    (* W-boson goldstone *)
    S[3] == {
        ClassName        -> GP,
        SelfConjugate    -> False,
        Goldstone        -> W,
        Mass             -> {MW, Internal},
        QuantumNumbers   -> {Q -> 1},
        Width            -> {WW, 2.085},
        PropagatorLabel  -> "GP",
        PropagatorType   -> D,
        PropagatorArrow  -> None,
        PDG              -> 251,
        ParticleName     -> "G+",
        AntiParticleName -> "G-",
        FullName         -> "GP"
    },
    (* Scalar Mediator *)
    S[4] == {
        ClassName        -> sm,
        SelfConjugate    -> True,
        Mass             -> {MSM, 1000.0},
        Width            -> {WSM,1.0},
        PropagatorLabel  -> "S",
        PropagatorType   -> D,
        PropagatorArrow  -> None,
        PDG              -> 54,
        ParticleName     -> "S",
        FullName         -> "S"
    },

    (**** UNPHYSICAL FIELDS ****)


    (* U(1)_Y hyper-charge gauge boson *)
    V[11] == {
        ClassName     -> B,
        Unphysical    -> True,
        SelfConjugate -> True,
        Definitions   -> { B[mu_] -> -sw Z[mu]+cw A[mu]}
    },
    (* SU(2)_L weak gauge bosons *)
    V[12] == {
        ClassName     -> Wi,
        Unphysical    -> True,
        SelfConjugate -> True,
        Indices       -> {Index[SU2W]},
        FlavorIndex   -> SU2W,
        Definitions   -> { Wi[mu_,1] -> (Wbar[mu]+W[mu])/Sqrt[2],
                           Wi[mu_,2] -> (Wbar[mu]-W[mu])/(I*Sqrt[2]),
                           Wi[mu_,3] -> cw Z[mu] + sw A[mu]}
    },
    (* U(1)_Y hyper-charge ghosts *)
    U[11] == {
        ClassName     -> ghB,
        Unphysical    -> True,
        SelfConjugate -> False,
        Ghost         -> B,
        Definitions   -> { ghB -> -sw ghZ + cw ghA}
    },
    (* SU(2)_L weak ghosts *)
    U[12] == {
        ClassName     -> ghWi,
        Unphysical    -> True,
        SelfConjugate -> False,
        Ghost         -> Wi,
        Indices       -> {Index[SU2W]},
        FlavorIndex   -> SU2W,
        Definitions   -> {ghWi[1] -> (ghWp+ghWm)/Sqrt[2],
                          ghWi[2] -> (ghWm-ghWp)/(I*Sqrt[2]),
                          ghWi[3] -> cw ghZ+sw ghA}
    },
    (* left-handed lepton doublet *)
    F[11] == {
        ClassName      -> LL,
        Unphysical     -> True,
        Indices        -> {Index[SU2D], Index[Generation]},
        FlavorIndex    -> SU2D,
        SelfConjugate  -> False,
        QuantumNumbers -> {Y -> -1/2},
        Definitions    -> {LL[sp1_,1,ff_] :> Module[{sp2}, ProjM[sp1,sp2] vl[sp2,ff]],
                           LL[sp1_,2,ff_] :> Module[{sp2}, ProjM[sp1,sp2] l[sp2,ff]]}
    },
    (* right-handed leptons *)
    F[12] == {
        ClassName      -> lR,
        Unphysical     -> True,
        Indices        -> {Index[Generation]},
        FlavorIndex    -> Generation,
        SelfConjugate  -> False,
        QuantumNumbers -> {Y -> -1},
        Definitions    -> {lR[sp1_,ff_] :> Module[{sp2}, ProjP[sp1,sp2] l[sp2,ff]]}
    },
    (* left-handed quarks doublet *)
    F[13] == {
        ClassName      -> QL,
        Unphysical     -> True,
        Indices        -> {Index[SU2D], Index[Generation], Index[Colour]},
        FlavorIndex    -> SU2D,
        SelfConjugate  -> False,
        QuantumNumbers -> {Y -> 1/6},
        Definitions    -> {QL[sp1_,1,ff_,cc_] :> Module[{sp2}, ProjM[sp1,sp2] uq[sp2,ff,cc]],
                           QL[sp1_,2,ff_,cc_] :> Module[{sp2,ff2}, CKM[ff,ff2] ProjM[sp1,sp2] dq[sp2,ff2,cc]]}
    },
    (* right-handed up-type-quarks *)
    F[14] == {
        ClassName      -> uR,
        Unphysical     -> True,
        Indices        -> {Index[Generation], Index[Colour]},
        FlavorIndex    -> Generation,
        SelfConjugate  -> False,
        QuantumNumbers -> {Y -> 2/3},
        Definitions    -> {uR[sp1_,ff_,cc_] :> Module[{sp2}, ProjP[sp1,sp2] uq[sp2,ff,cc]]}
    },
    (* right-handed down-type-quarks *)
    F[15] == {
        ClassName      -> dR,
        Unphysical     -> True,
        Indices        -> {Index[Generation], Index[Colour]},
        FlavorIndex    -> Generation,
        SelfConjugate  -> False,
        QuantumNumbers -> {Y -> -1/3},
        Definitions    -> {dR[sp1_,ff_,cc_] :> Module[{sp2}, ProjP[sp1,sp2] dq[sp2,ff,cc]]}
    },
    (* Higgs-doublet  *)
    S[11] == {
        ClassName      -> Phi,
        Unphysical     -> True,
        Indices        -> {Index[SU2D]},
        FlavorIndex    -> SU2D,
        SelfConjugate  -> False,
        QuantumNumbers -> {Y -> 1/2},
        Definitions    -> {Phi[1] -> -I GP,
                           Phi[2] -> (vev + cmix * H - smix * sm + I G0) / Sqrt[2]}
    },
    S[12] == {
        ClassName      -> smp,
        Unphysical     -> True,
        SelfConjugate  -> True,
        Definitions    -> {smp -> cmix * sm + smix * H}
    }
};


GaugeXi[ V[1]  ] = GaugeXi[A];
GaugeXi[ V[2]  ] = GaugeXi[Z];
GaugeXi[ V[3]  ] = GaugeXi[W];
GaugeXi[ V[4]  ] = GaugeXi[G];
GaugeXi[ S[1]  ] = 1;
GaugeXi[ S[2]  ] = GaugeXi[Z];
GaugeXi[ S[3]  ] = GaugeXi[W];
GaugeXi[ U[1]  ] = GaugeXi[A];
GaugeXi[ U[2]  ] = GaugeXi[Z];
GaugeXi[ U[31] ] = GaugeXi[W];
GaugeXi[ U[32] ] = GaugeXi[W];
GaugeXi[ U[4]  ] = GaugeXi[G];


M$Parameters = {
    (* External parameters *)
    aEWM1 == {
        ParameterType    -> External,
        BlockName        -> SMINPUTS,
        OrderBlock       -> 1,
        Value            -> 127.9,
        InteractionOrder -> {QED,-2},
        Description      -> "Inverse of the EW coupling constant at the Z pole"
    },
    Gf == {
        ParameterType    -> External,
        BlockName        -> SMINPUTS,
        OrderBlock       -> 2,
        Value            -> 1.16637*^-5,
        InteractionOrder -> {QED,2},
        TeX              -> Subscript[G,f],
        Description      -> "Fermi constant"
    },
    aS    == {
        ParameterType    -> External,
        BlockName        -> SMINPUTS,
        OrderBlock       -> 3,
        Value            -> 0.1184,
        InteractionOrder -> {QCD,2},
        TeX              -> Subscript[\[Alpha],s],
        Description      -> "Strong coupling constant at the Z pole"
    },
    ymdo == {
        ParameterType -> External,
        BlockName     -> YUKAWA,
        OrderBlock    -> 1,
        Value         -> 5.04*^-3,
        Description   -> "Down Yukawa mass"
    },
    ymup == {
        ParameterType -> External,
        BlockName     -> YUKAWA,
        OrderBlock    -> 2,
        Value         -> 2.55*^-3,
        Description   -> "Up Yukawa mass"
    },
    yms == {
        ParameterType -> External,
        BlockName     -> YUKAWA,
        OrderBlock    -> 3,
        Value         -> 0.101,
        Description   -> "Strange Yukawa mass"
    },
    ymc == {
        ParameterType -> External,
        BlockName     -> YUKAWA,
        OrderBlock    -> 4,
        Value         -> 1.27,
        Description   -> "Charm Yukawa mass"
    },
    ymb == {
        ParameterType -> External,
        BlockName     -> YUKAWA,
        OrderBlock    -> 5,
        Value         -> 4.7,
        Description   -> "Bottom Yukawa mass"
    },
    ymt == {
        ParameterType -> External,
        BlockName     -> YUKAWA,
        OrderBlock    -> 6,
        Value         -> 172,
        Description   -> "Top Yukawa mass"
    },
    yme == {
        ParameterType -> External,
        BlockName     -> YUKAWA,
        OrderBlock    -> 11,
        Value         -> 5.11*^-4,
        Description   -> "Electron Yukawa mass"
    },
    ymm == {
        ParameterType -> External,
        BlockName     -> YUKAWA,
        OrderBlock    -> 13,
        Value         -> 0.10566,
        Description   -> "Muon Yukawa mass"
    },
    ymtau == {
        ParameterType -> External,
        BlockName     -> YUKAWA,
        OrderBlock    -> 15,
        Value         -> 1.777,
        Description   -> "Tau Yukawa mass"
    },
    (* CKM up-down-type-quark CKM *)
    Vud == {
        ParameterType -> External,
        BlockName     -> CKMBLOCK,
        OrderBlock    -> 1,
        Value         -> 0.974461,
        TeX           -> Subscript[V, udR],
        Description   -> "Magnitude of u-d CKM"
    },
    Vus == {
        ParameterType -> External,
        BlockName     -> CKMBLOCK,
        OrderBlock    -> 2,
        Value         -> 0.224529,
        TeX           -> Subscript[V, usR],
        Description   -> "Magnitude of u-s CKM"
    },
    Vub == {
        ParameterType -> External,
        BlockName     -> CKMBLOCK,
        OrderBlock    -> 3,
        Value         -> 0.00364284,
        TeX           -> Subscript[V, ubR],
        Description   -> "Magnitude of u-b CKM"
    },

    (* CKM charm-down-type-quark CKM *)
    Vcd == {
        ParameterType -> External,
        BlockName     -> CKMBLOCK,
        OrderBlock    -> 4,
        Value         -> 0.224379,
        TeX           -> Subscript[V, cdR],
        Description   -> "Magnitude of c-d CKM"
    },
    Vcs == {
        ParameterType -> External,
        BlockName     -> CKMBLOCK,
        OrderBlock    -> 5,
        Value         -> 0.97359,
        TeX           -> Subscript[V, csR],
        Description   -> "Magnitude of c-s CKM"
    },
    Vcb == {
        ParameterType -> External,
        BlockName     -> CKMBLOCK,
        OrderBlock    -> 6,
        Value         -> 0.0421456,
        TeX           -> Subscript[V, cbR],
        Description   -> "Magnitude of c-b CKM"
    },

    (* CKM top-down-type-quark CKM *)
    Vtd == {
        ParameterType -> External,
        BlockName     -> CKMBLOCK,
        OrderBlock    -> 7,
        Value         -> 0.00896391,
        TeX           -> Subscript[V, tdR],
        Description   -> "Magnitude of t-d CKM"
    },
    Vts == {
        ParameterType -> External,
        BlockName     -> CKMBLOCK,
        OrderBlock    -> 8,
        Value         -> 0.0413421,
        TeX           -> Subscript[V, tsR],
        Description   -> "Magnitude of t-s CKM"
    },
    Vtb == {
        ParameterType -> External,
        BlockName     -> CKMBLOCK,
        OrderBlock    -> 9,
        Value         -> 0.999105,
        TeX           -> Subscript[V, tbR],
        Description   -> "Magnitude of t-b CKM"
    },
    (* Dark matter parameters *)
    smix == {
        ParameterType    -> External,
        BlockName        -> DMBLOCK,
        OrderBlock       -> 1,
        InteractionOrder -> {NPMIX, 1},
        Value            -> 1.0*^-3,
        TeX              -> Sin[Subscript[\[Theta], mix]],
        Description      -> "Sine of higgs-scalar-mediator mixing angle"
    },
    gSXX == {
        ParameterType    -> External,
        BlockName        -> DMBLOCK,
        OrderBlock       -> 2,
        InteractionOrder -> {NP, 1},
        Value            -> 1.0,
        TeX              -> Subscript[g, SXX],
        Description      -> "coupling of DM to scalar-mediator"
    },

    (* Internal Parameters *)
    aEW == {
        ParameterType    -> Internal,
        Value            -> 1 / aEWM1,
        InteractionOrder -> {QED,2},
        TeX              -> Subscript[\[Alpha], EW],
        Description      -> "Electroweak coupling contant"
    },
    MW == {
        ParameterType -> Internal,
        Value         -> Sqrt[MZ^2 / 2 + Sqrt[MZ^4 / 4 - Pi / Sqrt[2] * aEW / Gf * MZ^2]],
        TeX           -> Subscript[M,W],
        Description   -> "W mass"
    },
    sw2 == {
        ParameterType -> Internal,
        Value         -> 1 - (MW / MZ)^2,
        Description   -> "Squared Sin of the Weinberg angle"
    },
    ee == {
        ParameterType    -> Internal,
        Value            -> Sqrt[4 * Pi * aEW],
        InteractionOrder -> {QED,1},
        TeX              -> e,
        Description      -> "Electric coupling constant"
    },
    cw == {
        ParameterType -> Internal,
        Value         -> Sqrt[1 - sw2],
        TeX           -> Subscript[c,w],
        Description   -> "Cosine of the Weinberg angle"
    },
    sw == {
        ParameterType -> Internal,
        Value         -> Sqrt[sw2],
        TeX           -> Subscript[s,w],
        Description   -> "Sine of the Weinberg angle"
    },
    gw == {
        ParameterType    -> Internal,
        Definitions      -> {gw -> ee / sw},
        InteractionOrder -> {QED,1},
        TeX              -> Subscript[g,w],
        Description      -> "Weak coupling constant at the Z pole"
    },
    g1 == {
        ParameterType    -> Internal,
        Definitions      -> {g1 -> ee / cw},
        InteractionOrder -> {QED,1},
        TeX              -> Subscript[g,1],
        Description      -> "U(1)Y coupling constant at the Z pole"
    },
    gs == {
        ParameterType    -> Internal,
        Value            -> Sqrt[4 * Pi * aS],
        InteractionOrder -> {QCD,1},
        TeX              -> Subscript[g,s],
        ParameterName    -> G,
        Description      -> "Strong coupling constant at the Z pole"
    },
    vev == {
        ParameterType    -> Internal,
        Value            -> 2 * MW * sw / ee,
        InteractionOrder -> {QED,-1},
        Description      -> "Higgs vacuum expectation value"
    },
    lam == {
        ParameterType    -> Internal,
        Definitions      -> {lam -> (1 - smix^2) * (MH^2 + (-1 + 1/(1 - smix^2)) * MSM^2) / (2 * vev^2)},
        InteractionOrder -> {QED, 2},
        Description      -> "Higgs quartic coupling"
    },
    muH == {
        ParameterType -> Internal,
        Definitions   -> {muH -> (Sqrt[1 - smix^2] * Sqrt[MH^2 + (-1 + 1/(1 - smix^2)) * MSM^2])/Sqrt[2]},
        TeX           -> \[Mu],
        Description   -> "Coefficient of the quadratic piece of the Higgs potential"
    },
    yl == {
        ParameterType    -> Internal,
        Indices          -> {Index[Generation], Index[Generation]},
        Definitions      -> {yl[i_?NumericQ, j_?NumericQ] :> 0 /; UnsameQ[i, j]},
        Value            -> {yl[1,1] -> Sqrt[2] * yme / vev,
                             yl[2,2] -> Sqrt[2] * ymm / vev,
                             yl[3,3] -> Sqrt[2] * ymtau / vev},
        InteractionOrder -> {QED, 1},
        ParameterName    -> {yl[1,1] -> ye,
                             yl[2,2] -> ym,
                             yl[3,3] -> ytau},
        TeX              -> Superscript[y, l],
        Description      -> "Lepton Yukawa couplings"
    },
    yu == {
        ParameterType    -> Internal,
        Indices          -> {Index[Generation], Index[Generation]},
        Definitions      -> {yu[i_?NumericQ, j_?NumericQ] :> 0 /; UnsameQ[i, j]},
        Value            -> {yu[1,1] -> Sqrt[2] * ymup / vev,
                             yu[2,2] -> Sqrt[2] * ymc / vev,
                             yu[3,3] -> Sqrt[2] * ymt / vev},
        InteractionOrder -> {QED, 1},
        ParameterName    -> {yu[1,1] -> yup,
                             yu[2,2] -> yc,
                             yu[3,3] -> yt},
        TeX              -> Superscript[y, u],
        Description      -> "Up-type Yukawa couplings"
    },
    yd == {
        ParameterType    -> Internal,
        Indices          -> {Index[Generation], Index[Generation]},
        Definitions      -> {yd[i_?NumericQ, j_?NumericQ] :> 0 /; UnsameQ[i, j]},
        Value            -> {yd[1,1] -> Sqrt[2] * ymdo / vev,
                             yd[2,2] -> Sqrt[2] * yms / vev,
                             yd[3,3] -> Sqrt[2] * ymb / vev},
        InteractionOrder -> {QED, 1},
        ParameterName    -> {yd[1,1] -> ydo,
                             yd[2,2] -> ys,
                             yd[3,3] -> yb},
        TeX              -> Superscript[y, d],
        Description      -> "Down-type Yukawa couplings"
    },
    CKM == {
        ParameterType -> Internal,
        Indices       -> {Index[Generation], Index[Generation]},
        Unitary       -> True,
        Value         -> {CKM[1,1] -> Vud, CKM[1,2] -> Vus, CKM[1,3] -> Vub,
                          CKM[2,1] -> Vcd, CKM[2,2] -> Vcs, CKM[2,3] -> Vcb,
                          CKM[3,1] -> Vtd, CKM[3,2] -> Vts, CKM[3,3] -> Vtb},
        TeX           -> Superscript[V,CKM],
        Description   -> "CKM-Matrix"
    },
    (* dark matter parameters *)
    cmix == {
        ParameterType    -> Internal,
        Definitions      -> {cmix -> Sqrt[1 - smix^2]},
        TeX              -> Cos[Subscript[\[Theta], mix]],
        Description      -> "Cosine of higgs-scalar-mediator mixing angle"
    },
    c1 == {
        ParameterType    -> Internal,
        Definitions      -> {c1 -> ((MH - MSM) * (MH + MSM) * Sqrt[1 - smix^2] * smix) / vev},
        InteractionOrder -> {NPMIX, 1},
        TeX              -> Subscript[c, 1],
        Description      -> "coupling of scalar-mediator to Higgs"
    },
    MSp == {
        ParameterType -> Internal,
        Definitions   -> {MSp -> (Sqrt[1 - smix^2] * Sqrt[-MH^2 + MSM^2 + (MH^2 + MSM^2)/(1 - 2*smix^2)])/Sqrt[1 + 1/(1 - 2*smix^2)]},
        TeX           -> Subscript[c, 1],
        Description   -> "coupling of scalar-mediator to Higgs"
    }
};

(**** LAGRANGIAN TOOLS****)

repCKMUnitary = {
    CKM[a_, b_] * Conjugate[CKM[a_, c_]] -> IndexDelta[b, c],
    CKM[b_, a_] * Conjugate[CKM[c_, a_]] -> IndexDelta[b, c]
};

(* generators in real representation: see below 90 of arXiv:1101.4665 *)
GenReal[1] = (gw/2) * {{0, 0, 0, -1}, {0, 0, 1, 0}, {0, -1, 0, 0}, {1, 0, 0, 0}};
GenReal[2] = (gw/2) * {{0, 0, 1, 0}, {0, 0, 0, 1}, {-1, 0, 0, 0}, {0, -1, 0, 0}};
GenReal[3] = (gw/2) * {{0, -1, 0, 0}, {1, 0, 0, 0}, {0, 0, 0, 1}, {0, 0, -1, 0}};
GenReal[4] = (g1/2) * {{0, -1, 0, 0}, {1, 0, 0, 0}, {0, 0, 0, -1}, {0, 0, 1, 0}};

(* Higgs field in 4-d vector *)
phiv = {GP + GPbar, -I * (GP - GPbar), vev + H, G0} / Sqrt[2];
phivb = {0, 0, vev, 0} / Sqrt[2];

(* ghost fields in 4-d vector *)
ghv = {ghWi[1], ghWi[2], ghWi[3], ghB};
ghvbar = {ghWibar[1], ghWibar[2], ghWibar[3], ghBbar};

feynmanGaugeRules := If[Not[FeynmanGauge], {G0|GP|GPbar ->0}, {}];


(********************)
(**** LAGRANGIAN ****)
(********************)


LGauge := Block[{mu,nu,ii,aa,kinB,kinW,kinG},
    kinB = -1/4 * FS[B,mu,nu] * FS[B,mu,nu];
    kinW = -1/4 * FS[Wi,mu,nu,ii] * FS[Wi,mu,nu,ii];
    kinG = -1/4 * FS[G,mu,nu,aa] * FS[G,mu,nu,aa];

    ExpandIndices[kinB + kinW + kinG, FlavorExpand->SU2W]
];

LFermions := Block[{mu,kin},
    kin = I * QLbar.Ga[mu].DC[QL, mu];
    kin += I * LLbar.Ga[mu].DC[LL, mu];
    kin += I * uRbar.Ga[mu].DC[uR, mu];
    kin += I * dRbar.Ga[mu].DC[dR, mu];
    kin += I * lRbar.Ga[mu].DC[lR, mu];

    ExpandIndices[kin, FlavorExpand->{SU2W,SU2D}]/.repCKMUnitary
];

LHiggs := Block[{ii,mu,kin,pot},
    (* Higgs kinetic term *)
    kin = DC[Phibar[ii], mu] * DC[Phi[ii], mu];
    (* Higgs potential *)
    pot = -muH^2 * Phibar[ii] * Phi[ii] + lam * Phibar[ii] * Phi[ii] * Phibar[jj] * Phi[jj];
    (* expand out adjoint and doublet indices *)
    ExpandIndices[kin - pot, FlavorExpand->{SU2D,SU2W}]/.feynmanGaugeRules
];

LYukawa := Block[{sp,ii,jj,cc,ff1,ff2,ff3,yuk},
    (* up-type-quark yukawa interactions *)
    yuk = -yu[ff1, ff2] * QLbar[sp, ii, ff1, cc].uR[sp, ff2, cc] * Phibar[jj] * Eps[ii, jj];
    (* down-type-quark yukawa interactions *)
    yuk += -yd[ff2, ff3] * CKM[ff1, ff2] * QLbar[sp, ii, ff1, cc].dR[sp, ff3, cc] * Phi[ii];
    (* down-type-lepton yukawa interactions *)
    yuk += -yl[ff1, ff3] * LLbar[sp, ii, ff1].lR[sp, ff3] * Phi[ii];

    (* expand doublet indices *)
    yuk = ExpandIndices[yuk, FlavorExpand -> SU2D];
    (* apply unitarity *)
    yuk = yuk/.repCKMUnitary;
    (* add hermitian conjugate *)
    (yuk + HC[yuk])/.feynmanGaugeRules
];

LGhost := Block[{kin,mass,Mg,mu,ii},
    (* kinetic terms *)
    kin = -ghBbar.del[DC[ghB,mu],mu] - ghWibar[ii].del[DC[ghWi[ii],mu],mu];

    (* ghost fields assembled into vector *)
    ghv = {ghWi[1], ghWi[2], ghWi[3], ghB};
    ghvbar = {ghWibar[1], ghWibar[2], ghWibar[3], ghBbar};

    (* ghost mass + goldstone interaction matrix *)
    Mg = Table[Dot[Dot[GenReal[a], phivb], Dot[GenReal[b], phiv]], {a, 1, 4}, {b, 1, 4}];
    mass = - ghvbar.Mg.ghv;

    (* sum over adjoint indices *)
    ExpandIndices[kin + mass, FlavorExpand->SU2W]
];

LDM := Block[{kin,mass,int,mu,ii},
    kin = I * chibar.Ga[mu].del[chi, mu] + 1/2 * del[smp, mu] * del[smp, mu];
    mass = -MDM * chibar.chi - 1/2 * MSp^2 * smp^2;
    int = -gSXX * smp * chibar.chi - c1 * smp * Phibar[ii] * Phi[ii];

    ExpandIndices[kin + mass + int, FlavorExpand->SU2D]/.feynmanGaugeRules
];

LSM:= LGauge + LFermions + LHiggs + LYukawa + LGhost + LDM;
